plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id 'maven-publish'
}

group = 'me.epic'
version = '1.0.6'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://repo.epicebic.xyz/public"
    }
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven { url 'https://jitpack.io' }
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    implementation 'me.epic:epicspigotlib:1+'
    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.jetbrains:annotations:19.0.0'
    implementation 'commons-io:commons-io:2.11.0'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    compileOnly 'me.clip:placeholderapi:2.11.2'
    implementation 'me.epic:betteritemconfig:2.0.1-SNAPSHOT'
    implementation "com.tchristofferson:ConfigUpdater:+"
}


shadowJar {
    archiveFileName = "SimpleChatGames-${project.version}.jar"
    relocate 'me.epic.spigotlib', 'me.epic.chatgames.spigotlib'
    relocate 'org.bstats', 'me.epic.chatgames.bstats'
    destinationDirectory = file("./target/")
    archiveClassifier.set('')
    minimize {
        exclude('me.epic.spigotlib.utils.**')
    }
}

build {
    dependsOn shadowJar
}

tasks.register('release') {
    finalizedBy(clean, build, publish)
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifacts = [shadowJar, javadocJar, sourcesJar]
            version = version + "-SNAPSHOT"
        }
    }
    repositories {
        maven {
            credentials {
                username = "epic"
                password = System.getenv("EPICREPO_SECRET")
            }
            url = 'https://repo.epicebic.xyz/public/'
        }
    }
}